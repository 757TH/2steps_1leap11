title: Foreign Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: month_actions += 1; foreign_policy_timer += 8
view-if: spd_in_government = 1 and foreign_minister_party = "SPD" and  foreign_policy_timer <= 0
card-image: img/portraits/BreitscheidRudolf.jpg
go-to: fp_menu

= Foreign Policy

What should our foreign policy priority be?

@fp_menu

- @improve_west
- @improve_east
- @improve_soviet
- @customs_union
- @negotiate
- @reduce_reparations
- @expose
- @expose_nm
- @european_union
- @european_union_2
- @eu_expansion
- @spain_cw_support
- @mussolini_rebels
- @concordat_negotiate
- @concordat
- @root: Do not enact any policies for now.
- @easy_discard

@improve_west
title: Improve our relations with the Western Allies.
on-arrival: west_relation += 2; reparations_negotiation += 1

The key to peace in Europe is a rapproachment between France and Germany. We are taking steps towards that rapproachment. [? if young_plan_seen == 0 : This is difficult to do when France is occupying a major portion of Germany's territory, but we are working on that part. ?]

@improve_east
title: Improve our relations with our eastern neighbors.
on-arrival: east_relation += 2;

We also aim to improve relations with our eastern neighbors, Poland and Czechoslovakia, who are righfully terrified of German aggression. Hopefully we can convince them of our peaceful intentions.

@improve_soviet
title: Improve our relations with the Soviet Union.
subtitle: We could also broach the question of the KPD...

The Soviet Union also suffered from the international order created by the Treaty of Versailles; they have been participating in our secret rearmament program. The Treaty of Rapallo in 1922 affirmed our friendly relations.

The Soviets are asking for economic and developmental assistance. They are still a poor country on the whole, and are desperately trying to catch up to the western powers. There is a chance that our anti-communist coalition partners would be upset, but we could assuage them by asking the Soviets for secret assistance in training and equiping the Reichswehr.

- @soviet_economic_aid_yes_mil: Provide aid, in exchange for Reichswehr assistance.
- @soviet_economic_aid_yes: Provide aid, in exchange for only good relations.
- @soviet_economic_aid_no: Don't provide aid.

@soviet_economic_aid_yes_mil
choose-if: budget >= 1
subtitle: -1 budget
on-arrival: budget -= 1; soviet_relation += 1; reichswehr_strength += 20; reichswehr_loyalty += 0.05; foreign_goal_completed += 1; world_war_two += 2 if reichswehr_strength >= 250

The Soviets are pleased that we have sent them aid, and have agreed to help train our military. The Reichswehr appreciates our support. 

[? if reichswehr_strength >= 250: Germany's neighbors consider our actions to be a violation of the Treaty of Versailles and threaten repercussions. ?]

@soviet_economic_aid_yes
choose-if: budget >= 1
subtitle: -1 budget
on-arrival: budget -= 1; soviet_relation += 2; coalition_dissent += 1; foreign_goal_completed += 1

The Soviets are pleased that we have sent them aid, and have not asked for anything specific in exchange. Our coalition partners are less pleased.

- @kpd: Ask if Moscow could help repair our relations with the KPD.
- @wittorf: Do the Soviets know about the Wittorf Affair? Do they know that we know?
- @root: Do not mention the KPD.

@soviet_economic_aid_no

The Soviets are slightly upset, but they seemed to expect this answer.


@kpd
on-arrival: kpd_relation += 8*(1-dissent); communist_coalition += 1 if kpd_foreign_seen == 0; kpd_foreign_seen = 1

We also mention the KPD in our conversations with the Soviet ambassador. They claim that the KPD is entirely independent of the Soviet government, and that the conflict is Thälmann's doing. But, soon after our conversation we notice a slight improvement in attitudes from the KPD.

@wittorf
view-if: wittorf_secret = 1 and ((year = 1929 and month < 6) or year = 1928) and wittorf_soviet_union = 0
choose-if: historical_mode = 0 and nightmare = 0
unavailable-subtitle: It is impossible that Stalin and the Comintern could be moved on their policy.
on-arrival: wittorf_soviet_union = 1; kpd_relation += 6*(1-dissent); kpd_foreign_seen = 1
max-visits: 1

Of course Stalin and the various leaders of the Comintern know about the Wittorf Affair, but they do not seem to know that we know as well. The Comintern was planning to build a narrative around the affair that absolves Thälmann, but perhaps now they will be less inclined to defend him.


@customs_union
title: Form a customs union with Austria?
subtitle: This will likely incur the wrath of France.
unavailable-subtitle: This is superseded by the European Union.
choose-if: eu = 0
view-if: black_thursday_seen = 1 and customs_union = 0

The right has long desired closer ties with Austria, with the eventual goal of the merger of the two German-speaking countries. A customs union with Austria is the first step towards this goal. Additionally, many believe that a union with Austria would improve our current economic circumstances.

However, the Allies, especially France, are likely to object. This agreement would break the terms of the Versailles Treaty, and could lead to economic sanctions, further hurting our economy.

- @customs_union_2: Yes, do it!
- @fp_menu: No, we should do something else.

@customs_union_2
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2; z_relation += 6; dvp_relation += 6; coalition_dissent -= 1 if coalition_dissent > 0; coup_progress -= 1
max-visits: 1

We have formed a customs union with Austria! The right-wing parties are praising us for this unexpected turn of events.

Immediately, France and other neighboring countries lodge their protests. The longer-term response is yet to be seen.


@negotiate
title: Negotiate regarding our remaining obligations from the Treaty of Versailles.
subtitle: [? if young_plan_seen == 1 : After passing the Young Plan, France has committed to withdrawing from the Rhineland. Now, we must see if they can reduce our reparations further, given the economic depression. ?]
view-if: reparations > -2 and reparations_negotiation < 3
on-arrival: west_relation += 1; reparations_negotiation += 1; reparations_negotiation += 1 if pacifism >= 3; reparations_negotiations += 1 if nationalism <= 50; reparations_negotiation += 1 if west_relation >= 3

Negotiations procede smoothly for the most part. The Western powers are convinced of Germany's sincerity and goodwill. [? if nationalism <= 50 or pacifism >= 3 : They are pleased to see that we are renouncing militarism. ?]


@reduce_reparations
title: The Western Allies are finally willing to pause our reparations payments!
subtitle: Our negotiations have been successful.
view-if: reparations_negotiation >= 3 and black_thursday_seen == 1 and reparations > -2
on-arrival: west_relation += 1; reparations = -2; budget += 2 if hoover_memorandum_seen == 0; budget += 1 if hoover_memorandum_seen == 1; reparations_stopped = 1; coalition_dissent -= 1 if coalition_dissent > 0; workers_spd += 8*(1-dissent); old_middle_spd += 5*(1-dissent); new_middle_spd += 5*(1-dissent); rural_spd += 5*(1-dissent); coup_progress -= 2 if coup_progress > 2
max-visits: 1

Finally, given both the economic circumstances and our continued negotiations, the Western Allies are willing to pause our reparations payments! Much of the credit must go to American president Hoover, who has been friendlier towards us than the Europeans.

This has reduced tensions within our government, and our party has gotten credit for the achievement.

@expose
title: Expose the secret rearmament plans of the previous foreign ministry!
view-if: rearmament_exposed == 0 and nightmare <= 0
on-arrival: west_relation += 2; east_relation += 1; nationalism -= 5*(1-dissent); pro_republic += 5*(1-dissent); coalition_dissent += 1; rearmament_exposed += 1; reparations_negotiation += 3; coup_progress += 2

The previous foreign ministry, despite its benevolent and peaceful face, was secretly helping the Reichswehr to build up its forces, in contravention of the Treaty of Versailles.

The Allies are grateful of our exposure and repudiation of militarism. Our partners in the coalition are less supportive.

@expose_nm
title: Expose the secret rearmament plans of the previous foreign ministry!
view-if: rearmament_exposed == 0 and nightmare >= 1
on-arrival: west_relation += 2; east_relation += 1; nationalism -= 5*(1-dissent); pro_republic += 5*(1-dissent); coalition_dissent += 1; rearmament_exposed += 1; reparations_negotiation += 3; coup_progress += 2

The previous foreign ministry, despite its benevolent and peaceful face, was secretly helping the Reichswehr to build up its forces, in contravention of the Treaty of Versailles.

While Allies are grateful of our exposure, they are suspicious of our motives and request to station international observers throughout Reichswehr to safeguard against further backsliding into militarism. 

- @allow_observers: Agree to observers, despite strenous objections of other parties.
- @deny_observers: That is a step too far.

@allow_observers
on-arrival: coalition_dissent += 2; coup_progress += 2

The Allies appreciate our cooperation. Our partners in the coalition declare us traitors to the nation.

@deny_observers
on-arrival: west_relation -= 1; reparations_negotiation -= 1

The Allies are disappointed by our refusal.

@economic_conference
title: Organize an international economic conference to deal with the Depression.
unavailable-subtitle: [? if west_relation < 4 : Our relations with the West are not good enough. ?]
view-if: black_friday_seen == 1 and crisis_urgency >= 1 and reparations <= -2
choose-if: west_relation >= 4
on-arrival: economic_conference = 1; west_relation += 1; east_relation += 1
max-visits: 1


@concordat_negotiate
title: Negotiate a Concordat between the Germany and the Vatican.
view-if: prussian_concordat = 1 and reichskonkordat = 0 and reichskonkordat_progress < 4
on-arrival: reichskonkordat_progress += 2; reichskonkordat_progress -= 0.5 if abortion_rights; reichskonkordat_progress -= 0.5 if family_law; reichskonkordat_progress -= 0.5 if w_rights=1; z_relation += 3*(1-dissent)

The Roman Catholic Church is interested in furthering relations with the German Republic, seeing us as preferable to the old anti-Catholic policies of the Empire. We are working towards the signing of a Concordat, an agreement between the Vatican and the Republic that formally recognizes the Church's rights and interests in the country. The Center Party is especially interested in making this agreement.

[? if family_law : Our liberalized divorce policies make it difficult to build relations with the Vatican. ?] [? if abortion : Our legalization of abortion make it difficult to connect with the Church. ?] [? if w_rights = 1: Our constitutional amendment allowing abortion makes negotiations more difficult. ?]

@concordat
title: We can sign a Concordat with the Vatican!
view-if: reichskonkordat_progress >= 4 and reichskonkordat = 0
on-arrival: reichskonkordat = 1; z_relation += 15*(1-dissent); catholics_spd += 8*(1-dissent); catholics_z += 5 if in_weimar_coalition or in_grand_coalition or in_popular_front;
achievement: reichskonkordat

We have successfully signed a Concordat between Germany and the Roman Catholic Church! This treaty defines a formal relationship between the Church and the German state, guaranteeing freedom of worship for Catholics, and the right of the church to appoint their own clergy. Catholics in Germany are pleased, as are their representatives in the Center Party.

@spain_cw_support
title: Support the Spanish Republicans.
subtitle: They will need our help if they are to triumph in the civil war. -1 resources.
view-if: spain_cw = 1 and spain_cw_over = 0 and resources > 0
on-arrival: spain_support += 1 if spain_eu_support < 1; spain_eu_support += 1 if spain_church_centrist = 1; spain_eu_support += 1 if spain_centrist = 1; spain_franco = 0; resources -= 1;

We have sent support to the Spanish Republicans. They are grateful for our support and promise to collaborate if they triumph.

@mussolini_rebels
title: Support resistance against Mussolini's regime.
subtitle: Can he be overthrown?
view-if: mussolini_overthrown != 1 and abyssinia_long_war = 1 and resources > 0
on-arrival: mussolini_unrest = 1; resources -= 1;

With the ongoing war in Abyssinia, resistance against Mussolini's leadership is rising in Italy. We can help accelerate this and perhaps contribute to his overthrow.

@european_union
title: Take steps to form a "European Union".
subtitle: Is a "United States of Europe" possible?
unavailable-subtitle: [? if west_relation < 5 : Our relations with the West are not good enough. ?] [? if east_relation < 4 : Our relations with the East are not good enough. ?]
view-if: reparations <= -2 and eu_progress < 2
choose-if: west_relation >= 5 and east_relation >= 4
on-arrival: eu_progress += 1; west_relation += 1

Forming a "United States of Europe" has long been part of the Social Democratic foreign program. Finally, we are taking steps towards it. Given our positive relations with the other countries of Europe, we can begin to hold multilateral negotiations for more European federalism. It will take time to hammer out the exact shape of this future "European Union", however.


@european_union_2
title: We can sign a treaty to form a European Union!
view-if: west_relation >= 5 and east_relation >= 4 and eu_progress >= 2
on-arrival: eu = 1; unemployed -= 2 if unemployed >= 10; inflation += 2 if inflation <= -2; inflation -= 1 if inflation >= 4.5; workers_spd += 5*(1-dissent); new_middle_spd += 5*(1-dissent); old_middle_spd += 5*(1-dissent); rural_spd += 4*(1-dissent); catholics_spd += 4*(1-dissent); unemployed_spd += 4*(1-dissent); pro_republic += 5; nationalism -= 5; budget += 1; eu_expansion = 0; eu_members = "Germany, France, UK, Ireland, Poland, Czechoslovakia, Austria, Netherlands, Belgium, Luxembourg, Denmark";
max-visits: 1
achievement: eu

Along with France, Great Britain, and many other states of Western and Eastern Europe, we have signed a treaty to form a European Union! It is hoped that Europe has learned the lessons of the Great War; never again will there be another war that tears the continent apart.

A major component of the union is the free movement of people and goods across the countries of Europe, as well as the stabilization of currency exchange rates. This provides new markets for German products and new sources of investment.

Fascist Italy remains a problem, and the Soviet Union also lies outside the system. But for now, there is new hope for peace and prosperity throughout the continent.

@eu_expansion
title: Expand the European Union.
view-if: eu = 1 and eu_expansion < 11

While we have formed an European Union, it does not yet cover the whole continent. We can solve this by expanding it.

- @baltics: Invite the Baltic states to the EU.
- @scandinavia: Invite the Scandinavian nations to the EU.
- @spain: Invite Spain to the EU.
- @italy: Invite Italy to the EU.
- @balkans: Invite the Balkan nations to the EU.
- @greece: Invite Greece to the EU.
- @portugal: Invite Portugal to the EU.
- @hungary: Invite Hungary to the EU.
- @ussr: Invite the Soviet Union to the EU.
- @switzerland: Invite Switzerland to the EU.

@baltics
view-if: year < 1940
unavailable-subtitle: They are currently under Soviet occupation.
max-visits: 1
on-arrival: baltics_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

The Baltic states have accepted! They hope that this will help protect them against Soviet aggression.

@scandinavia
max-visits: 1
on-arrival: scandinavia_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 2;

Scandinavia has accepted our invitation. As one of Europe's more peaceful and liberal areas, their membership is welcomed. [? if year < 1939: Finland's government, worried about potential Soviet aggression, is glad to have extra protection. ?]

@spain
choose-if: year > 1941 and spain_cw_over = 1 and republican_victory = 1 and francoist_victory = 0 and spain_eu_support > 0 and spain_support < 1
unavailable-subtitle: [? if year < 1936 : The government is too unstable. ?] [? if spain_cw = 1 and spain_cw_over = 0 : The ongoing civil war makes this impossible. ?] [? if francoist_victory = 1 : We cannot invite a dictator into the EU. ?] [? if republican_victory = 1 and year < 1942 : They are undergoing reconstruction and not ready to join. ?] [? if spain_eu_support < 1 and spain_cw_over = 1 : Our EU partners consider Spain too radical. ?]
max-visits: 1
on-arrival: spain_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

Spain has accepted our invitation. With the nation mostly reconstructed from the civil war's destruction, EU membership will help the reconstruction finish and the nation prosper.

@italy
choose-if: mussolini_overthrown = 1
unavailable-subtitle: We cannot invite a fascist dictator.
max-visits: 1
on-arrival: italy_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

Italy has accepted our invitation for EU membership. Having turned a page on their dictatorship and monarchy, they are hoping that this will help them improve their country.

@balkans
max-visits: 1
on-arrival: balkans_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

The Balkan nations, except Hungary, have accepted. Bulgaria, Romania, Albania and Yugoslavia have joined.

@greece
choose_if: year < 1936
unavailable-subtitle: The Hellenic Republic has been overthrown.
max-visits: 1
on-arrival: greece_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

Greece has accepted our invitation. With them still being in a poor economic state, their membership should help them recover.

@portugal
choose-if: disband_military > 1
unavailable-subtitle: [? if year < 1933 : The Ditadura Nacional is not willing to cooperate with socialists. ?] [? if year > 1932 : The Estado Novo is unwilling to cooperate with socialists. ?]
max-visits: 1
on-arrival: portugal_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

If you are seeing this, something has gone wrong.

@hungary
choose-if: disband_military > 1
unavailable-subtitle: Horthy does not wish to cooperate.
on-arrival: hungary_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

If you are seeing this, something has gone wrong.

@ussr
choose-if: disband_military > 1
unavailable-subtitle: Both our EU partners and the Soviets are firmly against the idea.
max-visits: 1
on-arrival: ussr_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

If you are seeing this, something has gone wrong.

@switzerland
choose-if: disband_military > 1
unavailable-subtitle: The majority of the Swiss population does not support this.
max-visits: 1
on-arrival: switzerland_eu = 1; eu_expansion += 1; pro_republic += 5; nationalism -= 5; pacifism += 1;

If you are seeing this, something has gone wrong.
